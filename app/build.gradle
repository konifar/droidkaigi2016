import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.vanniktech.android.apk.size'
apply plugin: 'com.github.ben-manes.versions'

// Manifest version
def versionMajor = 1
def versionMinor = 1
def versionPatch = 5

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def packageName = "io.github.droidkaigi.confsched"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "CONFERENCE_TIMEZONE", "\"Asia/Tokyo\""

        // Referred to https://github.com/circleci/EspressoSample to make the Espresso tests
        // work on CircleCI.
        DdmPreferences.setTimeOut(60000)

        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            storeFile rootProject.file("release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "droidkaigi"
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true

            manifestPlaceholders = [
                    mapsApiKey: System.getenv('ANDROID_MAPS_API_KEY') != null ? System.getenv('ANDROID_MAPS_API_KEY') : android_maps_api_key
            ]
            buildConfigField("String", "GA_TRACKING_ID", "\"${ga_tracking_id_debug}\"")
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    mapsApiKey: System.getenv('ANDROID_MAPS_API_KEY_RELEASE') != null ? System.getenv('ANDROID_MAPS_API_KEY_RELEASE') : android_maps_api_key
            ]
            buildConfigField("String", "GA_TRACKING_ID", "\"${ga_tracking_id_release}\"")
        }
    }

    productFlavors {
        develop {
            applicationId "${packageName}.develop"
        }
        production {
            applicationId packageName
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress 'apk'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:support-annotations:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"
    compile "com.android.support:cardview-v7:${support_lib_version}"
    compile "com.android.support:recyclerview-v7:${support_lib_version}"
    compile "com.android.support:customtabs:${support_lib_version}"
    compile "com.android.support:support-vector-drawable:${support_lib_version}"
    compile "com.android.support:animated-vector-drawable:${support_lib_version}"

    compile "com.google.android.gms:play-services-analytics:${play_services_lib_version}"
    compile "com.google.android.gms:play-services-maps:${play_services_lib_version}"

    compile 'com.google.dagger:dagger:2.7'
    apt 'com.google.dagger:dagger-compiler:2.7'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile "com.jakewharton.threetenabp:threetenabp:1.0.4"
    compile "org.parceler:parceler-api:1.0.4"
    apt "org.parceler:parceler:1.0.4"

    compile 'com.github.gfx.android.orma:orma:3.0.0-rc2'
    apt 'com.github.gfx.android.orma:orma-processor:3.0.0-rc2'

    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.2.0'

    compile 'com.rejasupotaro:kvs-schema:5.0.1'
    apt 'com.rejasupotaro:kvs-schema-compiler:5.0.1'

    compile "com.squareup.picasso:picasso:2.5.2"
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.google.android:flexbox:0.2.3'
    compile 'com.github.jd-alexander:LikeButton:0.2.0'
    compile 'net.opacapp:multiline-collapsingtoolbar:1.0.0'
    compile('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true;
    }
    compile 'com.booking:rtlviewpager:1.0.1'

    compile 'com.google.android.exoplayer:exoplayer:r1.5.9'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    debugCompile 'com.facebook.stetho:stetho:1.4.1'

    testCompile 'com.github.gfx.android.robolectricinstrumentation:robolectric-instrumentation:3.0.8'
    testCompile 'junit:junit:4.12'
    testCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile 'org.mockito:mockito-all:1.10.19'
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
